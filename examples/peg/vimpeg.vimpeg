" <definition>       ::= <label> <mallet> <expression> <callback>?
" <expression>       ::= <sequence> ( <or> <sequence> )*
" <sequence>         ::= <prefix>*
" <prefix>           ::= <not>? <suffix>
" <suffix>           ::= <primary> ( <question> | <star> | <plus> )?
" <primary>          ::= <label> !<mallet> | <open> <expression> <close> | <regex>
" <callback>         ::= <right_arrow> <identifier>
" <label>            ::= <lt> <identifier> <gt>
" <identifier>       ::= <ident_start> <ident_cont>?
" <ident_cont>       ::= '\w\+'
" <ident_start>      ::= '\h'
" <regex>            ::= <dquoted_string> | <squoted_string>
" <dquoted_string>   ::= <dquote> ( <double_backslash> | <escaped_dquote> | '[^"]' )* <dquote>
" <squoted_string>   ::= <squote> ( "[^']" | <double_squote> )* <squote>
" <escaped_dquote>   ::= <backslash> <dquote>
" <double_backslash> ::= <backslash> <backslash>
" <backslash>        ::= '\'
" <dquote>           ::= '"'
" <double_squote>    ::= ''''''
" <squote>           ::= "'"
" <right_arrow>      ::= '->'
" <mallet>           ::= '::='
" <or>               ::= '|'
" <not>              ::= '!'
" <question>         ::= '?'
" <star>             ::= '\*'
" <plus>             ::= '+'
" <close>            ::= ')'
" <open>             ::= '('
" <gt>               ::= '>'
" <lt>               ::= '<'
